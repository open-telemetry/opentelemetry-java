plugins {
    id "java"
    id "ru.vyarus.animalsniffer"
}

description = 'OpenTelemetry Integration Tests'
ext.moduleName = "io.opentelemetry.integration.tests"

// create a single Jar with all dependencies
task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test.onlyIf {
    property('enable.docker.tests') == "true" 
}

dependencies {
    api project(':opentelemetry-api')

    implementation project(':opentelemetry-sdk'),
        project(':opentelemetry-exporters-jaeger'), 
        libraries.grpc_protobuf,
        libraries.protobuf,
        libraries.protobuf_util,
        "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation libraries.testcontainers,
        libraries.rest_assured

    signature "org.codehaus.mojo.signature:java18:1.0@signature"
    signature "net.sf.androidscents.signature:android-api-level-24:7.0_r2@signature"

    tasks.withType(Test) {
        dependsOn fatJar
        systemProperty 'archive.name', fatJar.outputs.files[0].path
    }
}

