description = "OpenTelemetry All"
ext.moduleName = "io.opentelemetry.all"

def subprojects = [
        project(':opentelemetry-api'),
        project(':opentelemetry-context-prop'),
        project(':opentelemetry-contrib-runtime-metrics'),
        project(':opentelemetry-contrib-trace-utils'),
        project(':opentelemetry-exporters-inmemory'),
        project(':opentelemetry-exporters-jaeger'),
        project(':opentelemetry-opentracing-shim'),
        project(':opentelemetry-sdk'),
        project(':opentelemetry-sdk-contrib-async-processor'),
        project(':opentelemetry-sdk-contrib-testbed'),
]

// A subset of subprojects for which we want to publish javadoc.
def subprojects_javadoc = [
        project(':opentelemetry-api'),
        project(':opentelemetry-context-prop'),
        project(':opentelemetry-contrib-runtime-metrics'),
        project(':opentelemetry-contrib-trace-utils'),
        project(':opentelemetry-exporters-inmemory'),
        project(':opentelemetry-exporters-jaeger'),
        project(':opentelemetry-opentracing-shim'),
        project(':opentelemetry-sdk'),
        project(':opentelemetry-sdk-contrib-async-processor'),
        project(':opentelemetry-sdk-contrib-testbed'),
]

for (subproject in rootProject.subprojects) {
    if (subproject == project) {
        continue
    }
    evaluationDependsOn(subproject.path)
}

dependencies {
    compile subprojects
}

javadoc {
    classpath = files(subprojects_javadoc.collect { subproject ->
        subproject.javadoc.classpath
    })
    for (subproject in subprojects_javadoc) {
        if (subproject == project) {
            continue
        }
        source subproject.javadoc.source
        options.links subproject.javadoc.options.links.toArray(new String[0])
    }
}

task jacocoMerge(type: JacocoMerge) {
    dependsOn(subprojects.jacocoTestReport.dependsOn)
    mustRunAfter(subprojects.jacocoTestReport.mustRunAfter)
    destinationFile = file("${buildDir}/jacoco/test.exec")
    executionData = files(subprojects.jacocoTestReport.executionData)
            .filter { f -> f.exists() }
}

jacocoTestReport {
    dependsOn(jacocoMerge)
    reports {
        xml.enabled = true
        html.enabled = true
    }

    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    classDirectories.from = files(classDirectories.files.collect {
        fileTree(dir: it)
    })
}