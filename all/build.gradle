plugins {
    id "java"
}

description = "OpenTelemetry All"
ext.moduleName = "io.opentelemetry.all"

def subprojects = [
        project(':opentelemetry-api'),
        project(':opentelemetry-context'),
        project(':opentelemetry-extension-runtime-metrics'),
        project(':opentelemetry-extension-trace-propagators'),
        project(':opentelemetry-extension-trace-utils'),
        project(':opentelemetry-extension-annotations'),
        project(':opentelemetry-exporter-jaeger'),
        project(':opentelemetry-exporter-logging'),
        project(':opentelemetry-exporter-otlp'),
        project(':opentelemetry-exporter-prometheus'),
        project(':opentelemetry-exporter-zipkin'),
        project(':opentelemetry-opencensus-shim'),
        project(':opentelemetry-opentracing-shim'),
        project(':opentelemetry-sdk-common'),
        project(':opentelemetry-sdk-metrics'),
        project(':opentelemetry-sdk-metrics-v2'),
        project(':opentelemetry-sdk-testing'),
        project(':opentelemetry-sdk-trace'),
        project(':opentelemetry-sdk'),
        project(':opentelemetry-sdk-extension-async-processor'),
        project(':opentelemetry-sdk-extension-aws'),
        project(':opentelemetry-sdk-extension-jaeger-remote-sampler'),
        project(':opentelemetry-sdk-extension-logging'),
        project(':opentelemetry-sdk-extension-otproto'),
        project(':opentelemetry-sdk-extension-resources'),
        project(':opentelemetry-sdk-extension-tracing-incubator'),
        project(':opentelemetry-sdk-extension-zpages'),
]

for (subproject in rootProject.subprojects) {
    if (subproject == project) {
        continue
    }
    evaluationDependsOn(subproject.path)
}

dependencies {
    compile subprojects
    testCompile libraries.archunit
}

task jacocoMerge(type: JacocoMerge) {
    dependsOn(subprojects.jacocoTestReport.dependsOn)
    mustRunAfter(subprojects.jacocoTestReport.mustRunAfter)
    destinationFile = file("${buildDir}/jacoco/test.exec")
    executionData = files(subprojects.jacocoTestReport.executionData)
            .filter { f -> f.exists() }
}

jacocoTestReport {
    dependsOn(jacocoMerge)
    reports {
        xml.enabled = true
        html.enabled = true
    }

    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    classDirectories.from = files(classDirectories.files.collect {
        fileTree(dir: it, excludes: [
                // Ignore generated code
                "io/opentelemetry/exporters/jaeger/proto/**"
        ])
    })
}
