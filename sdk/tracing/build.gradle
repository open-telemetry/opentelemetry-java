plugins {
    id "java"
    id "maven-publish"

    id "me.champeau.gradle.jmh"
    id "ru.vyarus.animalsniffer"
}

description = 'OpenTelemetry SDK Tracing'
ext.moduleName = "io.opentelemetry.sdk.trace"
ext.propertiesDir = "build/generated/properties/io/opentelemetry/sdk/trace"

dependencies {
    api project(':opentelemetry-api'),
            project(':opentelemetry-sdk-common')

    implementation libraries.guava

    annotationProcessor libraries.auto_value

    testAnnotationProcessor libraries.auto_value
    testCompileOnly libraries.auto_value_annotation

    testCompile project(path: ':opentelemetry-sdk-common', configuration: 'testClasses')

    testImplementation project(':opentelemetry-testing-internal')
    testImplementation libraries.junit_pioneer

    signature "net.sf.androidscents.signature:android-api-level-24:7.0_r2@signature"
}

sourceSets {
    main {
        output.dir("build/generated/properties", builtBy: 'generateVersionResource')
    }
}

animalsniffer {
    // Don't check sourceSets.jmh and sourceSets.test
    sourceSets = [
            sourceSets.main
    ]
}

task generateVersionResource {
    doLast {
        def folder = file(propertiesDir)
        folder.mkdirs()
        def propertiesFile = new File(folder.getAbsolutePath(), "version.properties")
        propertiesFile.write("sdk.version=${project.version}")
    }
}
