plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.jmh"
    id "ru.vyarus.animalsniffer"
}

description = 'OpenTelemetry SDK For Tracing'
ext.moduleName = "io.opentelemetry.sdk.trace"
ext.propertiesDir = "build/generated/properties/io/opentelemetry/sdk/trace"

dependencies {
    api project(':opentelemetry-api-trace'),
            project(':opentelemetry-sdk-common')

    // We do allow enabling metrics for span processors if the user enables it, but we don't want
    // the user to automatically have metrics on their compile classpath, so we use both
    // compileOnly and implementation.
    compileOnly project(':opentelemetry-api-metrics')
    implementation project(':opentelemetry-api-metrics')

    annotationProcessor libraries.auto_value

    testAnnotationProcessor libraries.auto_value
    testCompileOnly libraries.auto_value_annotation

    testCompile project(path: ':opentelemetry-sdk-common', configuration: 'testClasses')

    testImplementation libraries.guava
    testImplementation project(':opentelemetry-sdk-testing')
    testImplementation libraries.junit_pioneer

    signature libraries.android_signature
}

sourceSets {
    main {
        output.dir("build/generated/properties", builtBy: 'generateVersionResource')
    }
}

animalsniffer {
    // Don't check sourceSets.jmh and sourceSets.test
    sourceSets = [
            sourceSets.main
    ]
}

task generateVersionResource {
    doLast {
        def folder = file(propertiesDir)
        folder.mkdirs()
        def propertiesFile = new File(folder.getAbsolutePath(), "version.properties")
        propertiesFile.write("sdk.version=${project.version}")
    }
}
